plugins {
    id 'java'
    id 'maven'  // Required for jitpack.io to do a: ./gradlew install
    id 'org.jetbrains.kotlin.jvm' version '1.3.71'
    id "com.github.ben-manes.versions" version "0.28.0"
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

repositories {
    maven { url 'https://kotlin.bintray.com/ktor' }
    maven { url 'https://kotlin.bintray.com/kotlinx' }
    maven { url 'https://kotlin.bintray.com/kotlin-js-wrappers' }
    maven { url 'https://maven-central.storage-download.googleapis.com/repos/central/data/' }
    jcenter()
    maven { url = 'https://jitpack.io' }
}

def mainName = 'com.github.readingbat.Main'
def appName = 'server'

sourceCompatibility = 1.8
targetCompatibility = 1.8

description = 'ReadingBat Core'
group 'com.github.readingbat'
version '1.0.4'

sourceSets {
    main.kotlin.srcDirs = main.java.srcDirs = ['src']
    test.kotlin.srcDirs = test.java.srcDirs = ['test']
    main.resources.srcDirs = ['resources']
    test.resources.srcDirs = ['testresources']
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    implementation "com.github.pambrose.common-utils:core-utils:${utils_version}"
    implementation "com.github.pambrose.common-utils:script-utils:${utils_version}"

    implementation "io.ktor:ktor-server-cio:$ktor_version"
    implementation "io.ktor:ktor-server-core:$ktor_version"
    implementation "io.ktor:ktor-html-builder:$ktor_version"
    implementation "org.jetbrains:kotlin-css:$css_version"
    implementation "org.jetbrains:kotlin-css-jvm:$css_version"
    implementation "io.ktor:ktor-metrics:$ktor_version"
    implementation "io.ktor:ktor-client-core:$ktor_version"
    implementation "io.ktor:ktor-client-core-jvm:$ktor_version"
    implementation "io.ktor:ktor-client-cio:$ktor_version"

    implementation "org.apache.commons:commons-text:$commons_version"
    implementation "com.vladsch.flexmark:flexmark-all:$flexmark_version"

    implementation "ch.qos.logback:logback-classic:${logback_version}"
    implementation "org.slf4j:jul-to-slf4j:${slf4j_version}"
    implementation "io.github.microutils:kotlin-logging:${logging_version}"

    testImplementation "io.ktor:ktor-server-tests:$ktor_version"
    testImplementation "org.amshove.kluent:kluent:${kluent_version}"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junit_version}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junit_version}"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

/*
 * Copyright Â© 2020 Paul Ambrose (pambrose@mac.com)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

// Include build uberjars in heroku deploy
task stage(dependsOn: ['uberjar', 'build', 'clean'])
build.mustRunAfter clean

shadowJar {
    mergeServiceFiles()
}

task uberjar(type: Jar, dependsOn: shadowJar) {
    archiveFileName = 'server.jar'
    manifest {
        attributes('Implementation-Title': appName)
        attributes('Implementation-Version': version)
        attributes('Built-Date': new Date())
        attributes('Built-JDK': System.getProperty('java.version'))
        attributes('Main-Class': mainName)
    }
    from zipTree(shadowJar.archiveFile)
}

compileKotlin {
    kotlinOptions.jvmTarget = '1.8'
    kotlinOptions.freeCompilerArgs += ['-Xuse-experimental=kotlin.time.ExperimentalTime',
                                       '-Xuse-experimental=io.ktor.util.KtorExperimentalAPI',
                                       '-Xuse-experimental=kotlin.ExperimentalStdlibApi',
                                       '-XXLanguage:+InlineClasses']
}

compileTestKotlin {
    kotlinOptions.jvmTarget = '1.8'
    kotlinOptions.freeCompilerArgs += ['-Xuse-experimental=kotlin.time.ExperimentalTime',
                                       '-Xuse-experimental=io.ktor.util.KtorExperimentalAPI',
                                       '-Xuse-experimental=kotlin.ExperimentalStdlibApi',
                                       '-XXLanguage:+InlineClasses']
}

test {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        exceptionFormat "full"
        showStandardStreams = true
    }
}