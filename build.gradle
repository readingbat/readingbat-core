plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.3.71'
    id "com.github.ben-manes.versions" version "0.28.0"
}

repositories {
    maven { url = 'https://maven-central.storage-download.googleapis.com/repos/central/data/' }
    maven { url 'https://kotlin.bintray.com/ktor' }
    maven { url 'https://kotlin.bintray.com/kotlinx' }
    maven { url 'https://kotlin.bintray.com/kotlin-js-wrappers' }
    jcenter()
    maven { url = 'https://jitpack.io' }
}

def mainName = 'com.github.pambrose.readingbat.Main'
def appName = 'server'

def junitVersion = '5.6.1'
def kluentVersion = '1.60'
def logbackVersion = '1.2.3'
def loggingVersion = '1.7.9'
def slf4jVersion = '1.7.28'
def utilsVersion = '70bc014'

sourceCompatibility = 1.8
targetCompatibility = 1.8

description = 'ReadingBat Core'
group 'com.github.pambrose.readingbat'
version '0.0.1'

sourceSets {
    main.kotlin.srcDirs = main.java.srcDirs = ['src']
    test.kotlin.srcDirs = test.java.srcDirs = ['test']
    main.resources.srcDirs = ['resources']
    test.resources.srcDirs = ['testresources']
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    implementation "com.github.pambrose.common-utils:core-utils:${utilsVersion}"

    implementation "io.ktor:ktor-server-cio:$ktor_version"
    implementation "io.ktor:ktor-server-core:$ktor_version"
    implementation "io.ktor:ktor-html-builder:$ktor_version"
    implementation "org.jetbrains:kotlin-css:$css_version"
    implementation "org.jetbrains:kotlin-css-jvm:$css_version"
    implementation "io.ktor:ktor-metrics:$ktor_version"
    implementation "io.ktor:ktor-client-core:$ktor_version"
    implementation "io.ktor:ktor-client-core-jvm:$ktor_version"
    implementation "io.ktor:ktor-client-cio:$ktor_version"
    implementation 'com.vladsch.flexmark:flexmark-all:0.61.16'

    implementation "ch.qos.logback:logback-classic:${logbackVersion}"
    implementation "org.slf4j:jul-to-slf4j:${slf4jVersion}"
    implementation "io.github.microutils:kotlin-logging:${loggingVersion}"

    testImplementation "io.ktor:ktor-server-tests:$ktor_version"
    testImplementation "org.amshove.kluent:kluent:${kluentVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

compileKotlin {
    kotlinOptions.jvmTarget = '1.8'
    kotlinOptions.freeCompilerArgs += ['-Xuse-experimental=kotlin.time.ExperimentalTime',
                                       '-Xuse-experimental=kotlinx.serialization.UnstableDefault',
                                       '-Xuse-experimental=io.ktor.util.KtorExperimentalAPI',
                                       "-XXLanguage:+InlineClasses"]
}

compileTestKotlin {
    kotlinOptions.jvmTarget = '1.8'
    kotlinOptions.freeCompilerArgs += ['-Xuse-experimental=kotlin.time.ExperimentalTime']
}

test {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        exceptionFormat "full"
        showStandardStreams = true
    }
}